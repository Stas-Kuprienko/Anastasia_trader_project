plugins {
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.protobuf' version '0.9.4'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	//models
	implementation project(':model')

	//security
	implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation "org.springframework.security:spring-security-oauth2-jose:$security_oauth2_version"

    //kafka
	implementation "org.springframework.kafka:spring-kafka:$spring_kafka_version"

    //grpc framework
	implementation "io.grpc:grpc-netty-shaded:$rpc_version"
	implementation "io.grpc:grpc-protobuf:$rpc_version"
	implementation "io.grpc:grpc-stub:$rpc_version"
	implementation "net.devh:grpc-spring-boot-starter:$grpc_starter_version"

    //migration
	implementation "org.flywaydb:flyway-core:$flyway_version"
	implementation "org.flywaydb:flyway-database-postgresql:$flyway_version"

    //database
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	runtimeOnly "org.postgresql:postgresql:$postgresql_version"
	runtimeOnly "org.postgresql:r2dbc-postgresql:$r2dbc_postgresql_version"

	//xls
    implementation 'org.apache.poi:poi-ooxml:5.3.0'

    //tests
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.12'  // Protobuf compiler
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.56.0'  // gRPC codegen plugin
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.named('test') {
	useJUnitPlatform()
}
